# =========================================
# Rules for STEP3 
# Pool all tagAlign and subsample to NREADS
# =========================================
rule tagAlign_pool:
    input: expand("{{filedir}}/tagAlign/FINAL_{id}_" + cell + ".tagAlign.gz", id = ids)
    output: TA = "{filedir}/tagAlign/FINAL_" + EPITOPE + "_" + cell + ".tagAlign.gz"
    message: "== Pooling files to create {output.TA} =="
    run:
        shell("zcat " + " ".join(input) + " | gzip -c > {output.TA}")

# TODO MAKE POSSIBILITY OF UPSAMPLING!
rule tagAlign_subsample:
    input: TA = "{filedir}/tagAlign/{prefix}.tagAlign.gz"
    output: sub = "{filedir}/tagAlign/{prefix}.sub.tagAlign.gz"
    params: nreads = NREADS
    message: "== Subsampling {input.TA} to depth {params.nreads} =="
    shell: "zcat {input.TA} | shuf -n {params.nreads} | sort -k1,1V -k2,2g | gzip -c > {output.sub}"

rule tagAlign_spp_SCCA:
    input: TA = "{filedir}/tagAlign/{prefix}.tagAlign.gz"
    output: plot = "{filedir}/qc/{prefix}.cc.plot.pdf", stats = "{filedir}/qc/{prefix}.cc.qc"
    params: spp = config.SPPDIR, rdir="/broad/compbio/cboix/software/miniconda2/bin"
    message: "== Running SPP for SCCA on {input.TA} =="
    shell: """
    {params.rdir}/Rscript {params.spp}/run_spp_nodups.R -c={input.TA} -filtchr=chrM -x=-500:40 -savp={output.plot} -out={output.stats}
    sed -i -r 's/,[^\t]+//g' {output.stats}
    """

# TODO what's the difference here? -rf and the filt chr tag?
rule QC_tagAlign_spp_qconly:
    input: TA = "{filedir}/tagAlign/{prefix}.tagAlign.gz"
    output: plot = "{filedir}/qc/{prefix}.tagAlign.pdf", stats = "{filedir}/qc/{prefix}.stats.qc"
    params: spp = config.SPPDIR, rdir="/broad/compbio/cboix/software/miniconda2/bin"
    message: "-- spp QC stats on {input.TA} --"
    shell: "{params.rdir}/Rscript {params.spp}/run_spp_nodups.R -rf -c={input.TA} -x=-500:40 -savp={output.plot} -out={output.stats}"
